import wollok.game.*
import objetosEnJuego.proyectiles.*

import personajes.sobrevivientes.*
import personajes.personajes.*

import direcciones.*
import escenario.*

describe "Proyectiles"
{
	const flechaSimple = new FlechaSimple(betty, 10)
	
	test "Al instanciar un proyectil, se establece la direccion en la que avanza (orientacion del tirador) y la posicion (la del tirador) " 
	{
		// posicion de betty default (1,1)
		// orientacion de betty default abajo
		
		assert.equals(betty, flechaSimple.tirador())
		assert.equals(abajo, flechaSimple.direccion())
		assert.equals(game.at(1,1), flechaSimple.position())
	}
	
	test "Avanzar, mueve el proyectil una casilla en su direccion" 
	{
		flechaSimple.avanzar()
		
		assert.equals(game.at(1,0), flechaSimple.position())
	}
	
	test "Objetivo, el objetivo de la flecha es el objeto debajo de ella" 
	{
		jan.inicializar()
		game.addVisual(flechaSimple)
		
		assert.equals(jan.barraDeVida(), flechaSimple.objetivo())
	}
	
	test "Impactar, realiza daño y aplica el efecto al objeto superior en la casilla" 
	{
		jan.inicializar() // Vida actual de Jan = a Vida maxima (116.8 -> 117)
		
		game.addVisual(flechaSimple)
		flechaSimple.impactar() // Flecha Simple tiene 10 de daño y no tiene efecto -1 de daño recibido por calzoncillos
		
		assert.equals(108, jan.vidaActual())
	}
}

describe "Flechas"
{
	fixture
	{
		jan.inicializar()
	}
	
	test "Flecha Ignea, quema" 
	{
		const flecha = new FlechaIgnea(betty, 0)
		game.addVisual(flecha)
		flecha.impactar()
		
		assert.equals(tieneEstadoAlterado, jan.estadoDeQuemadura())
	}
	test "Flecha Gelida, escarcha" 
	{
		const flecha = new FlechaGelida(betty, 0)
		game.addVisual(flecha)
		flecha.impactar()
		
		assert.equals(tieneEstadoAlterado, jan.estadoDeEscarcha())
	}
	test "Flecha Oscura, cega" 
	{
		const flecha = new FlechaOscura(betty, 0)
		game.addVisual(flecha)
		flecha.impactar()
		
		assert.equals(tieneEstadoAlterado, jan.estadoDeCeguera())
	}
}
